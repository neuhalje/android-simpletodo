buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.2'
        classpath "com.jimdo.gradle:gradle-apt-plugin:0.5-SNAPSHOT"
    }
}
apply plugin: 'android'

apply from: "${rootDir}/android_common.gradle"
apply from: "${rootDir}/java_common.gradle"

repositories {
    mavenCentral()
}

// Signing of the release build with keys in an external configuration
// Configured in gradle.properties
if (project.hasProperty("MyProject.local")
        && new File(project.property("MyProject.local") + "/signing.gradle").exists()) {
    apply from: project.property("MyProject.local") + "/signing.gradle";
}

apply plugin: 'apt'


android {
    defaultConfig {
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-debug-rules.txt', 'proguard-rules.txt'
            zipAlign true
        }
    }
    productFlavors {
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        // JODA
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    def butterknifeVersion = "5.1.0"

    compile project.ext.shareddeps['dagger_rt']
    apt project.ext.shareddeps['dagger_apt']

    compile project.ext.shareddeps['joda_time']

    compile project(':libraries:todo-domain')
    compile 'com.android.support:support-v4:19.1.0'

    compile 'org.apache.commons:commons-lang3:3.3.2'

    compile 'com.squareup.flow:flow:0.7'
    compile 'com.squareup.mortar:mortar:0.16'

    compile "com.netflix.rxjava:rxjava-core:0.14.7"
    compile "com.google.code.gson:gson:2.2.4"

    compile "com.jakewharton:butterknife:${butterknifeVersion}"
    apt "com.jakewharton:butterknife:${butterknifeVersion}"

    compile ('de.keyboardsurfer.android.widget:crouton:1.8.4') {
    // exclusion is not neccessary, but generally a good idea.
    exclude group: 'com.google.android', module: 'support-v4'
  }
    compile project.ext.shareddeps['eventbus']
    compile 'net.sourceforge.findbugs:annotations:1.3.2'
    compile 'com.google.code.findbugs:jsr305:2.0.3'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task zip2jar(type: Zip, dependsOn: "compileReleaseJava") {
    from 'build/intermediates/classes/release'
    destinationDir = file('build/libs')
    extension = "jar"
}


